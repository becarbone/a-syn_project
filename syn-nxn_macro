//made by Bea Carbone - 2016
var color,  sourceSyndir, sourceSynSub, maskDir, zipDir, nxnInt, totalsynArea;
var areaROIarray, areaSynarray, synArea, synColor, synThresh;
var overArea, nxnColor, sourceNxndir, sourceNxnSub, nxnThresh, SynInt;

waitForUser("Image files must be saved by channel with subfolders for each condition. \nthe names must end in _Color.tif");

color = newArray("Green", "Red", "Blue", "Cyan");
Dialog.create("");
	Dialog.addChoice("Channel with a-syn", color, "Cyan");
	Dialog.addChoice("Channel with Nrx1a", color, "Red");
Dialog.show;

synColor = Dialog.getChoice();
nxnColor = Dialog.getChoice();

sourceSyndir = getDirectory("Choose folder for " + synColor +" source images");
sourceSynList = getFileList(sourceSyndir);
sourceNxndir = getDirectory("Choose folder for " + nxnColor +" source images");
sourceNxnList = getFileList(sourceNxndir);

if(File.isDirectory(sourceSynList[0])==1){
	exit("Please restructure your files to have nested folders with the different conditions");
}

maskDir = getDirectory("Choose folder for masks");
zipDir = getDirectory("Choose folder to save ROI information in");


Dialog.create("Threshold info");
	Dialog.addNumber("For "+ synColor,0);
	Dialog.addNumber("For "+ nxnColor,0);
Dialog.show;

synThresh = Dialog.getNumber();
nxnThresh = Dialog.getNumber();

run("Set Measurements...", "area integrated area_fraction limit redirect=None decimal=3");

ROIstring = nxnColor+" threshold: "+nxnThresh+"	"+synColor+" threshold: "+synThresh+"\nImage	area of fill(um) 	area of a-syn(um)	a-synArea(um)overNxnArea	%areaSynROIoverFillROI	%area_ROIrotate90R	%area_ROIrotate90L	IntensityNxn	IntensitySyn\n";


//enter thresholding values for two channels for mask settings 
p=0;
while(p<(sourceNxnList.length))	{
	sourceSynSub = getFileList(sourceSyndir+sourceSynList[p]);
	sourceNxnSub = getFileList(sourceNxndir+sourceNxnList[p]);
	if(File.exists(zipDir+sourceNxnList[p])!=1){
		File.makeDirectory(zipDir+sourceNxnList[p]);
	}
	ROIstring = ROIstring+sourceSynList[p]+"\n";
	for(l=0;l<sourceNxnSub.length;l++)	{
		//check and see if both nxn and a-syn files exists + open them if they do
		conditionNxn = getFileList(sourceNxndir+sourceNxnList[p]+sourceNxnSub[l]);
		conditionSyn = getFileList(sourceSyndir+sourceSynList[p]+sourceSynSub[l]);
		imgName = File.getName(sourceSyndir+sourceNxnList[p]+sourceNxnSub[p]);
		temp = substring(imgName,0,(lengthOf(imgName)-(lengthOf(nxnColor)+5)));
		imgName = temp;
		roiManager("reset");
		if((File.exists(sourceNxndir+sourceNxnList[p]+imgName+"_"+nxnColor+".tif") && File.exists(sourceSyndir+sourceSynList[p]+imgName+"_"+synColor+".tif")) == true)	{
			conditionNxn = getFileList(sourceNxndir+sourceNxnList[p]+sourceNxnSub[l]);
			conditionSyn = getFileList(sourceSyndir+sourceSynList[p]+sourceSynSub[l]);
			imgName = File.getName(sourceNxndir+sourceNxnSub[l]);
			temp = substring(imgName,0,(lengthOf(imgName)-(lengthOf(nxnColor)+5)));
			imgName = temp;
			//change names
			open(sourceNxndir+sourceNxnList[p]+sourceNxnSub[l]);
			run("Properties...", "channels=1 slices=1 frames=1 unit=micron pixel_width=0.1803752 pixel_height=0.1803752 voxel_depth=0.2092750");
			rename("Nxn");
			selectImage("Nxn");
			run("Duplicate...", "duplicate");
			selectImage("Nxn-1");
			rename("Nxn-unmask");
			selectImage("Nxn-unmask");
			open(sourceSyndir+sourceSynList[p]+sourceSynSub[l]);
			run("Properties...", "channels=1 slices=1 frames=1 unit=micron pixel_width=0.1803752 pixel_height=0.1803752 voxel_depth=0.2092750");
			rename("Syn");
			selectImage("Syn");
			run("Duplicate...", "duplicate");
			selectImage("Syn-1");
			rename("Syn-unmask");
			selectImage("Syn-unmask");
	
			//create mask for a-syn
			run("Clear Results");
			selectImage("Syn");
			run("Grays");
			run("32-bit");
			setThreshold(synThresh, 255);
			run("Convert to Mask");
			run("Convert to Mask");
			rename("Syn-mask");
			run("Make Binary");
			run("Create Selection");
			run("Measure");
			totalsynArea = 0;
			if(getResult("Area")>0)	{
				roiManager("Add");
				synArea = getResult("Area");
				totalsynArea = totalsynArea + synArea;
			}
			roiManager("Save", zipDir + sourceNxnList[p] + imgName +"_ROI.zip");
			selectImage("Syn-mask");
			saveAs("Tiff", maskDir+imgName+"_"+synColor);
			close();

			selectImage("Syn-unmask");
			run("Properties...", "channels=1 slices=1 frames=1 unit=micron pixel_width=0.1803752 pixel_height=0.1803752 voxel_depth=0.2092750");
			roiManager("Measure");
			if(getResult("Area")>0)	{
				SynInt = getResult("IntDen",0);
			}
			selectImage("Syn-unmask");
			close();
	
			//create mask for nxn
			roiManager("reset");
			run("Clear Results");
			selectImage("Nxn");
			run("Grays");
			run("32-bit");
			setThreshold(nxnThresh, 255);
			run("Convert to Mask");
			run("Convert to Mask");
			rename("Nxn-mask");
			run("Make Binary");
			run("Create Selection");
			run("Measure");
			if(getResult("Area")>0)	{
				roiManager("Add");
				nxnArea = getResult("Area");
			}

			selectImage("Nxn-unmask");
			run("Properties...", "channels=1 slices=1 frames=1 unit=micron pixel_width=0.1803752 pixel_height=0.1803752 voxel_depth=0.2092750");
			roiManager("Measure");
			if(getResult("Area")>0)	{
				nxnInt = getResult("IntDen",0);
			}
			run("Clear Results");
			selectImage("Nxn-mask");
			roiManager("reset");
			roiManager("open", zipDir + sourceNxnList[p] + imgName +"_ROI.zip");
			roiManager("measure");
			saveAs("Tiff", maskDir+imgName+"_"+nxnColor);
			overArea = 0;
			for(q=0;q<nResults;q++){
				overArea = overArea+getResult("Area");
			}

			run("Clear Results");
			selectImage(imgName+"_"+nxnColor+".tif");
			run("Rotate 90 Degrees Right");
			roiManager("measure");
			rotateAreaR = getResult("%Area");
			run("Clear Results");
			selectImage(imgName+"_"+nxnColor+".tif");
			run("Rotate 90 Degrees Left");
			run("Rotate 90 Degrees Left");
			roiManager("measure");
			rotateAreaL = getResult("%Area");

			close();
			selectImage("Nxn-unmask");
			close();
			ROIstring = ROIstring + imgName + "	" +nxnArea + "	" + synArea + "	" + (synArea*(overArea/100)) + "	" +overArea+  "	" +rotateAreaR+ "	" +rotateAreaL+ "	" +nxnInt+ "	" +synArea+"\n";
		
		}
	}
	p = p+1;
}
File.saveString(ROIstring, maskDir+"results.xls");
print(ROIstring);
